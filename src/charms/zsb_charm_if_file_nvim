#!/usr/bin/env zsh
set -o errexit
set -o pipefail


# (tightly coupled with tmux config, ripgrep, and nvim)
# for tmux conf, see https://github.com/EdgardoArriagada/dotFiles/blob/main/tmux/home/.tmux.conf

function loadFile() {
  # cargo
  export PATH=$PATH:~/.cargo/bin
  # fnm
  eval "$(fnm env --use-on-cd)"

  case $# in
    1) nvim ${1} ;;
    2) nvim ${1} +${2} ;;
  esac
}

local lines=( ${(s.ยง.)"${@}"} )
local cursorLine=${lines[${#lines}]}

if [[ -f ${cursorLine} ]]
  then loadFile ${cursorLine}; exit 0
fi

function findFileIndexInValidParagraph() {
  for ((i=${#lines}; i>0; i--)); do
    if [[ -f ${lines[${i}]} ]]
      then printf ${i}; return
      elif [[ ${lines[${i}]} =~ ^[0-9]+: ]]
        then continue
      else break
    fi
  done

  printf "Err"
}

local fileIndex=`findFileIndexInValidParagraph`

if [[ ${fileIndex} == "Err" ]]
  then exit 1
fi

# get number out of first number until ':' in a line using param expansion
local lineNumber=${cursorLine%%:*}

# if its a valid number, open the file
if [[ "$lineNumber" = <-> ]]
  then loadFile ${lines[${fileIndex}]} ${lineNumber}; exit 0
fi

exit 1
